{-
  TASK 2 - Matrix multiplier
  Write the method that multiplies one matrix by another.
  The 1st and 2nd parameter of the function will contain
  a matrix implemented using tuples (eg: [[1,2], [3,4]]).
  Matrices can be of different sizes, ranging from 4x4
  up to 32x32.
  Matrix can be rectangular where matrixA must to be of size n*m & matrixB of size m*p.
  After calculating the result matrix, the function should return final result (as tuple).
-}

() recv_internal() {
}

(int) length(tuple t) asm "TLEN";

;; testable
(tuple) matrix_multiplier(tuple matrixA, tuple matrixB) method_id {
  tuple answer = empty_tuple();
  int n = matrixA.length();
  int m = matrixB.length();
  int p = matrixB.at(0).length();
  int row = 0;
  repeat (n) {
    int col = 0;
    tuple row_tuple = empty_tuple();
    repeat (p) {
      int i = 0;
      int value = 0;
      repeat (m) {
        value += matrixA.at(row).at(i) * matrixB.at(i).at(col);
        i += 1;
      }
      row_tuple~tpush(value);
      col += 1;
    }
    answer~tpush(row_tuple);
    row += 1;
  }
  return answer;
}

